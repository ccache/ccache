%language=ANSI-C
%enum
%struct-type
%readonly-tables
%define hash-function-name confitems_hash
%define lookup-function-name confitems_get
%define initializer-suffix ,0,NULL,0,NULL
struct conf_item;
%%
base_dir,             0, ITEM_V(base_dir, env_string, absolute_path)
cache_dir,            1, ITEM(cache_dir, env_string)
cache_dir_levels,     2, ITEM_V(cache_dir_levels, unsigned, dir_levels)
compiler,             3, ITEM(compiler, string)
compiler_check,       4, ITEM(compiler_check, string)
compression,          5, ITEM(compression, bool)
compression_level,    6, ITEM(compression_level, int)
compression_type,     7, ITEM(compression_type, string)
cpp_extension,        8, ITEM(cpp_extension, string)
direct_mode,          9, ITEM(direct_mode, bool)
disable,             10, ITEM(disable, bool)
extra_files_to_hash, 11, ITEM(extra_files_to_hash, env_string)
hard_link,           12, ITEM(hard_link, bool)
hash_dir,            13, ITEM(hash_dir, bool)
ignore_headers_in_manifest, 14, ITEM(ignore_headers_in_manifest, env_string)
keep_comments_cpp,   15, ITEM(keep_comments_cpp, bool)
limit_multiple,      16, ITEM(limit_multiple, float)
log_file,            17, ITEM(log_file, env_string)
max_files,           18, ITEM(max_files, unsigned)
max_size,            19, ITEM(max_size, size)
path,                20, ITEM(path, env_string)
prefix_command,      21, ITEM(prefix_command, env_string)
prefix_command_cpp,  22, ITEM(prefix_command_cpp, env_string)
read_only,           23, ITEM(read_only, bool)
read_only_direct,    24, ITEM(read_only_direct, bool)
recache,             25, ITEM(recache, bool)
run_second_cpp,      26, ITEM(run_second_cpp, bool)
sloppiness,          27, ITEM(sloppiness, sloppiness)
stats,               28, ITEM(stats, bool)
temporary_dir,       29, ITEM(temporary_dir, env_string)
umask,               30, ITEM(umask, umask)
unify,               31, ITEM(unify, bool)
