%language=ANSI-C
%enum
%struct-type
%readonly-tables
%define hash-function-name confitems_hash
%define lookup-function-name confitems_get
%define initializer-suffix ,0,NULL,0,NULL
struct conf_item;
%%
base_dir,             0, ITEM_V(base_dir, env_string, absolute_path)
cache_dir,            1, ITEM(cache_dir, env_string)
cache_dir_levels,     2, ITEM_V(cache_dir_levels, unsigned, dir_levels)
compiler,             3, ITEM(compiler, string)
compiler_check,       4, ITEM(compiler_check, string)
compression,          5, ITEM(compression, bool)
compression_level,    6, ITEM(compression_level, unsigned)
cpp_extension,        7, ITEM(cpp_extension, string)
direct_mode,          8, ITEM(direct_mode, bool)
directives_only,      9, ITEM(directives_only, bool)
disable,             10, ITEM(disable, bool)
extra_files_to_hash, 11, ITEM(extra_files_to_hash, env_string)
hard_link,           12, ITEM(hard_link, bool)
hash_dir,            13, ITEM(hash_dir, bool)
ignore_headers_in_manifest, 14, ITEM(ignore_headers_in_manifest, env_string)
log_file,            15, ITEM(log_file, env_string)
max_files,           16, ITEM(max_files, unsigned)
max_size,            17, ITEM(max_size, size)
path,                18, ITEM(path, env_string)
prefix_command,      19, ITEM(prefix_command, env_string)
read_only,           20, ITEM(read_only, bool)
read_only_direct,    21, ITEM(read_only_direct, bool)
recache,             22, ITEM(recache, bool)
run_second_cpp,      23, ITEM(run_second_cpp, bool)
sloppiness,          24, ITEM(sloppiness, sloppiness)
stats,               25, ITEM(stats, bool)
temporary_dir,       26, ITEM(temporary_dir, env_string)
umask,               27, ITEM(umask, umask)
unify,               28, ITEM(unify, bool)
