%language=ANSI-C
%enum
%struct-type
%readonly-tables
%define hash-function-name confitems_hash
%define lookup-function-name confitems_get
%define initializer-suffix ,0,0,NULL,NULL,NULL
%{
#include "confitems.h"
#include "conf.h"

#undef bool
#define ITEM_ENTRY(name, type, verify_fn) \
	offsetof(struct conf, name), confitem_parse_ ## type, \
	confitem_format_ ## type, verify_fn
#define ITEM(name, type) \
	ITEM_ENTRY(name, type, NULL)
#define ITEM_V(name, type, verification) \
	ITEM_ENTRY(name, type, confitem_verify_ ## verification)
%}
struct conf_item;
%%
base_dir,             0, ITEM_V(base_dir, env_string, absolute_path)
cache_dir,            1, ITEM(cache_dir, env_string)
cache_dir_levels,     2, ITEM_V(cache_dir_levels, unsigned, dir_levels)
compiler,             3, ITEM(compiler, string)
compiler_check,       4, ITEM(compiler_check, string)
compression,          5, ITEM(compression, bool)
compression_level,    6, ITEM(compression_level, unsigned)
cpp_extension,        7, ITEM(cpp_extension, string)
debug,                8, ITEM(debug, bool)
direct_mode,          9, ITEM(direct_mode, bool)
disable,             10, ITEM(disable, bool)
extra_files_to_hash, 11, ITEM(extra_files_to_hash, env_string)
hard_link,           12, ITEM(hard_link, bool)
hash_dir,            13, ITEM(hash_dir, bool)
ignore_headers_in_manifest, 14, ITEM(ignore_headers_in_manifest, env_string)
keep_comments_cpp,   15, ITEM(keep_comments_cpp, bool)
limit_multiple,      16, ITEM(limit_multiple, double)
log_file,            17, ITEM(log_file, env_string)
max_files,           18, ITEM(max_files, unsigned)
max_size,            19, ITEM(max_size, size)
path,                20, ITEM(path, env_string)
pch_external_checksum, 21, ITEM(pch_external_checksum, bool)
prefix_command,      22, ITEM(prefix_command, env_string)
prefix_command_cpp,  23, ITEM(prefix_command_cpp, env_string)
read_only,           24, ITEM(read_only, bool)
read_only_direct,    25, ITEM(read_only_direct, bool)
recache,             26, ITEM(recache, bool)
run_second_cpp,      27, ITEM(run_second_cpp, bool)
sloppiness,          28, ITEM(sloppiness, sloppiness)
stats,               29, ITEM(stats, bool)
temporary_dir,       30, ITEM(temporary_dir, env_string)
umask,               31, ITEM(umask, umask)
unify,               32, ITEM(unify, bool)
